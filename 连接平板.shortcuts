on findLastTargetIndex(targetItem, itemList)
	set lastIndex to 0
	repeat with i from (count of itemList) to 1 by -1
		if item i of itemList contains targetItem then
			set lastIndex to i
			exit repeat
		end if
	end repeat
	return lastIndex
end findLastTargetIndex

-- 开始运行脚本
tell application "System Settings"
	activate
	delay 1
	tell application "System Events"
		tell process "系统设置"
			-- 点击左侧菜单中的“显示器”选项
			click menu item "显示器" of menu "显示" of menu bar item "显示" of menu bar 1
			delay 0.3
			
			-- 定位右侧区域
			tell group 1 of group 2 of splitter group 1 of group 1 of window "显示器"
				try
					delay 1
					-- 点击“添加”下拉按钮
					click pop up button "添加"
					
					-- 获取所有菜单项的名称
					set menuItems to name of menu items of menu "添加" of pop up button "添加"
					
					-- 查找最后一个 iPad 菜单项的索引
					set targetIndex to (my findLastTargetIndex("iPad", menuItems))
					
					-- 点击目标菜单项
					delay 0.3
					click menu item targetIndex of menu "添加" of pop up button "添加"
					
					-- 提示成功
					say "iPad 已连接"
				on error
					delay 0.5
					-- 提示未找到
					say "未找到可用的 iPad"
					
					-- 模拟唤醒 Mac mini 显示器
					say "尝试重新激活显示器"
					do shell script "caffeinate -u -t 2" -- 触发短时间唤醒
					
					-- 模拟按下 Command + F1 切换显示镜像
					tell application "System Events"
						key code 122 using {command down} -- Command + F1
					end tell
					
					delay 1
					say "请检查 iPad 是否重新连接"
				end try
			end tell
		end tell
	end tell
end tell

delay 1
beep 1
